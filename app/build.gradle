plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
    id 'net.ltgt.apt' version '0.9'
    id 'idea'
    // Intentionally disabled to avoid deprecation warnings. Enable on-demand.
    // id 'com.ofg.uptodate' version '1.6.2'
}

idea {
    module {
        sourceDirs += file(project.buildDir.absolutePath + '/generated/source/apt/main')
    }
}

compileGroovy {
    groovyOptions.configurationScript = file("$rootDir/compiler-config.groovy")
    groovyOptions.javaAnnotationProcessing = true
}

mainClassName = 'lt.vilnius.tvarkau.App'
shadowJar {
    baseName = 'tvarkau-vilniu'
    classifier = null
}
assemble.dependsOn shadowJar

// Skip unnecessary tasks from jar/application plugins
[jar, distZip, distTar, startScripts].each {
    it.enabled = false
}

def sparkVersion = '2.5.4'
dependencies {
    compile project(':libraries:util')
    compile "com.sparkjava:spark-core:${sparkVersion}"
    compile 'org.slf4j:slf4j-api:1.7.22'
    compile 'ch.qos.logback:logback-classic:1.1.8'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'org.postgresql:postgresql:9.4.1212'
    compile 'com.zaxxer:HikariCP:2.5.1'
    compile 'org.dalesbred:dalesbred:1.2.3'
    compile 'org.jetbrains:annotations:15.0'
    compile 'org.liquibase:liquibase-core:3.5.3'
    compile 'com.google.dagger:dagger:2.8'
    apt 'com.google.dagger:dagger-compiler:2.8'
}
